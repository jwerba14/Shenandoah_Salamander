
model{

############## Priors for parameters
######### All covariate relationships are held constant across years

# # Informative priors from Biotic model - taken from Empty/Alone sites
#
# alpha.psiS ~ dnorm(apSm, apStau)
# apStau <- 1/(apSsd*apSsd)
# alpha.psiC ~ dnorm(apCm, apCtau)
# apCtau <- 1/(apCsd*apCsd)
#
#
# alpha.survS ~ dnorm(asSm, asStau)
# asStau <- 1/(asSsd * asSsd)
# alpha.colS ~ dnorm(acSm, acStau)
# acStau <- 1/(acSsd * acSsd)
#
# alpha.survC ~ dnorm(asCm, asCtau)
# asCtau <- 1/(asCsd * asCsd)
# alpha.colC ~ dnorm(acCm, acCtau)
# acCtau <- 1/(acCsd * acCsd)
#
# ############## Detection parameters- Assume constant across years
#
# alpha.pS ~ dnorm(pSm, pStau)
# pStau <- 1/(pSsd * pSsd)
# alpha.pC ~ dnorm(pCm, pCtau)
# pCtau <- 1/(pCsd * pCsd)
#

# noninformative
alpha.psiS ~ dnorm(0, 0.368)
alpha.psiC ~ dnorm(0, 0.368)
alpha.survS ~ dnorm(0, 0.368)
alpha.colS ~ dnorm(0, 0.368)
alpha.survC ~ dnorm(0, 0.368)
alpha.colC ~ dnorm(0, 0.368)
alpha.pS ~ dnorm(0, 0.368)
alpha.pC ~ dnorm(0, 0.368)

###########################################
###########  ECOLOGY MODEL ################
###########################################

## SEASON 1

for(i in 1:N){    # Loop over sites
# True state model for the partially observed true state
# True occupancy z at site i  # Shenandoah sal
  z[i,1] ~ dbern(psiS[i])

    logit(psiS[i]) <- alpha.psiS

# True occupancy v at site i  # Cinereus sal
    v[i,1] ~ dbern(psiC[i])
    logit(psiC[i]) <- alpha.psiC

} #i

## SEASON 2 & ON

for(k in 2:Yr) {  # Loop over years
  for(i in 1:N) { # Loop over sites

    # True state model for the partially observed true state
    z[i,k] ~ dbern(muz[i,k-1])		# True occupancy z at site i   # Shenandoah sal

    v[i,k] ~ dbern(muv[i,k-1])		# True occupancy v at site i  # Cinereus sal

    muz[i,k-1] <- z[i,k-1] * survS[i, k-1] +
                (1- z[i,k-1]) * colS[i, k-1]

    logit(survS[i, k-1]) <- alpha.survS
    logit(colS[i, k-1]) <- alpha.colS

    muv[i,k-1] <- survC[i, k-1]* v[i,k-1] +
                 colC[i, k-1] * (1-v[i,k-1])

    logit(survC[i, k-1]) <- alpha.survC
    logit(colC[i, k-1]) <- alpha.colC

  } # i
} # k

###########################################
########  OBSERVATION MODEL ###############
###########################################

for(k in 1:Yr) {  # Loop over years
  for(i in 1:N) { # Loop over the R sites
    for(j in 1:J) { # Loop over replicates

    # Observation model for the actual observations
    yS[i,j,k] ~ dbern(eff.pS[i,j,k])	# Detection-nondetection at i and j

    # Observation model for the actual observations
    yC[i,j,k] ~ dbern(eff.pC[i,j,k])	# Detection-nondetection at i and j

    # Conditional detection probabilities
    eff.pS[i,j,k] <- z[i,k] * pS[i,j,k]

    # Conditional detection probabilities
    eff.pC[i,j,k] <-  pC[i,j,k] * v[i,k]

    # Use logit
    logit(pS[i,j,k]) <- alpha.pS
    logit(pC[i,j,k]) <- alpha.pC

    # Simulate data
    yS.new[i,j,k] ~ dbern(eff.pS[i,j,k])	# Simulate data
    yC.new[i,j,k] ~ dbern(eff.pC[i,j,k])	# Simulate data

    }
  }
}

############################################################
#########  Bayesian posterior predictive check ###############
###########################################################
for(k in 1:Yr) {  # Loop over years
  for(i in 1:N) { # Loop over the R sites
    for(j in 1:J){
      Presi.S[i,j,k] <- abs(yS[i,j,k]-eff.pS[i,j,k])	 # Absolute residual
      Presi.new.S[i,j,k] <- abs(yS.new[i,j,k]-eff.pS[i,j,k])	 # Absolute residual

      Presi.C[i,j,k] <- abs(yC[i,j,k]-eff.pC[i,j,k])	 # Absolute residual
      Presi.new.C[i,j,k] <- abs(yC.new[i,j,k]-eff.pC[i,j,k])	 # Absolute residual

    }
  }
}
zzzfit <-  sum(Presi.S[,,]) + sum(Presi.C[,,])
zzzfit.new <- sum(Presi.new.S[,,]) + sum(Presi.new.C[,,])

# Calculate the discrepancy measure, defined as the mean(p.fit > p.fitnew)
zzz.diff <- step(zzzfit - zzzfit.new)

}

