
model{

############## Priors for parameters 
######### All covariate relationships are held constant across years

# Informative priors from Biotic model - taken from Empty/Alone sites
## changed to wider on 10-15 because I couldn't get it to run

alpha.psiS ~ dnorm(0, 0.368)
#apStau <- 1/(apSsd*apSsd)
alpha.psiC ~ dnorm(0, 0.368)
#apCtau <- 1/(apCsd*apCsd)
    
    
alpha.survS ~ dnorm(0, 0.368)
#asStau <- 1/(asSsd * asSsd)
alpha.colS ~ dnorm(0, 0.368)
#acStau <- 1/(acSsd * acSsd)

alpha.survC ~ dnorm(0, 0.368)
#asCtau <- 1/(asCsd * asCsd)
alpha.colC ~ dnorm(0, 0.368)
#acCtau <- 1/(acCsd * acCsd)

############## Detection parameters- Assume constant across years

alpha.pS ~ dnorm(0, 0.368)
#pStau <- 1/(pSsd * pSsd)
alpha.pC ~ dnorm(0, 0.368)  
#pCtau <- 1/(pCsd * pCsd)

###########################################
###########  ECOLOGY MODEL ################
###########################################

## SEASON 1

for(i in 1:N){    # Loop over sites
# True state model for the partially observed true state
# True occupancy z at site i  # Shenandoah sal
  z[i,1] ~ dbern(psiS[i])	

    logit(psiS[i]) <- alpha.psiS 

# True occupancy v at site i  # Cinereus sal
    v[i,1] ~ dbern(psiC[i])
    logit(psiC[i]) <- alpha.psiC
    
} #i

## SEASON 2 & ON

for(k in 2:Yr) {  # Loop over years
  for(i in 1:N) { # Loop over sites

    # True state model for the partially observed true state
    z[i,k] ~ dbern(muz[i,k-1])		# True occupancy z at site i   # Shenandoah sal
    
    v[i,k] ~ dbern(muv[i,k-1])		# True occupancy v at site i  # Cinereus sal
    
    muz[i,k-1] <- z[i,k-1] * survS[i, k-1] + 
                (1- z[i,k-1]) * colS[i, k-1] 

    logit(survS[i, k-1]) <- alpha.survS
    logit(colS[i, k-1]) <- alpha.colS

    muv[i,k-1] <- survC[i, k-1]* v[i,k-1] + 
                 colC[i, k-1] * (1-v[i,k-1])

    logit(survC[i, k-1]) <- alpha.survC
    logit(colC[i, k-1]) <- alpha.colC

  } # i
} # k    

###########################################
########  OBSERVATION MODEL ###############
###########################################

for(k in 1:Yr) {  # Loop over years
  for(i in 1:N) { # Loop over the R sites
    for(j in 1:J) { # Loop over replicates

    # Observation model for the actual observations
    yS[i,j,k] ~ dbern(eff.pS[i,j,k])	# Detection-nondetection at i and j
    
    # Observation model for the actual observations
    yC[i,j,k] ~ dbern(eff.pC[i,j,k])	# Detection-nondetection at i and j
    
    # Conditional detection probabilities
    eff.pS[i,j,k] <- z[i,k] * pS[i,j,k]
    
    # Conditional detection probabilities
    eff.pC[i,j,k] <-  pC[i,j,k] * v[i,k] 
    
    # Use logit
    logit(pS[i,j,k]) <- alpha.pS
    logit(pC[i,j,k]) <- alpha.pC
    
    # Simulate data
    yS.new[i,j,k] ~ dbern(eff.pS[i,j,k])	# Simulate data
    yC.new[i,j,k] ~ dbern(eff.pC[i,j,k])	# Simulate data
    
    }
  }
}

}

